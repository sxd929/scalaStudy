
artifactory_contextUrl = 'https://relateiq.artifactoryonline.com/relateiq'

def artifactoryCredentials = {
    username "${artifactory_user}"
    password "${artifactory_password}"
}

artifactory {
    contextUrl = "${artifactory_contextUrl}"
    publish {
        repository {
            username = "${artifactory_user}"
            password = "${artifactory_password}"
            logger.info("Project version: " + project.version)
            repoKey = "libs-${project.version.endsWith('-SNAPSHOT') ? 'snapshots' : 'releases' }-local"
        }
        defaults {
            publications('mavenJava')
            properties = ['dev.team' : 'sfds']
            publishBuildInfo = true   //Publish build-info to Artifactory (true by default)
            publishArtifacts = true   //Publish artifacts to Artifactory (true by default)
            publishPom = true   //Publish generated POM files to Artifactory (true by default).
            publishIvy = false   //Publish generated Ivy descriptor files to Artifactory (true by default).
        }
    }

    // Redefine basic properties of the build info object
    clientConfig.setIncludeEnvVars(true)
    clientConfig.info.addEnvironmentProperty('built.on',new java.util.Date().toString())
    if (hasProperty('teamcity')) {
        clientConfig.info.setBuildName(teamcity.get("teamcity.buildType.id"))
        clientConfig.info.setBuildNumber(teamcity.get("build.number"))
    } else {
        clientConfig.info.setBuildName(project.name)
        clientConfig.info.setBuildNumber(new java.util.Random(System.currentTimeMillis()).nextInt(20000).toString())
    }
}


allprojects {
    repositories {
        maven {
            url "${artifactory_contextUrl}/libs-releases"
            credentials artifactoryCredentials
        }
        maven {
            url "${artifactory_contextUrl}/libs-snapshots"
            credentials artifactoryCredentials
        }
        maven {
            url "${artifactory_contextUrl}/plugins-releases"
            credentials artifactoryCredentials
        }
        maven {
            url "${artifactory_contextUrl}/plugins-snapshots"
            credentials artifactoryCredentials
        }
        mavenLocal()
        mavenCentral()
        maven { url "https://oss.sonatype.org/content/repositories/releases/" }
        maven { url "http://maven.twttr.com/" }
    }
}
